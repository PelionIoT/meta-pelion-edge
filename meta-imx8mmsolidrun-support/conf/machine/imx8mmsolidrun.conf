#@TYPE: Machine
#@NAME: SolidRun iMX8MM based platforms
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for SolidRun iMX8MM based platforms
#@MAINTAINER: Ricardo Salveti <ricardo@foundries.io>

MACHINEOVERRIDES =. "mx8:mx8m:mx8mm:imx8mm-lpddr4-evk:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa53.inc

IMX_DEFAULT_BSP = "nxp"

MACHINE_FEATURES += "pci wifi bluetooth optee"

UBOOT_MACHINE_imx8mmsolidrun = "imx8mm_solidrun_defconfig"
SPL_BINARY = "spl/u-boot-spl.bin"

ATF_PLATFORM = "imx8mm"
ATF_LOAD_ADDR = "0x920000"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = "lpddr4_pmu_train_1d_imem.bin lpddr4_pmu_train_1d_dmem.bin lpddr4_pmu_train_2d_imem.bin lpddr4_pmu_train_2d_dmem.bin"

# Set u-boot DTB
UBOOT_DTB_NAME = "imx8mm-solidrun.dtb"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "flash_evk"
IMX_BOOT_SOC_TARGET = "iMX8MM"

# Set Serial console
SERIAL_CONSOLES = "115200;ttymxc1"

LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""
IMX_BOOT_SEEK = "33"

OSTREE_KERNEL_ARGS_imx8mmsolidrun ?= "console=tty1 console=ttymxc1,115200 earlycon=ec_imx6q,0x30890000,115200 root=LABEL=otaroot rootfstype=ext4"

# device tree setup
PREFERRED_PROVIDER_virtual/dtb = "lmp-device-tree"
KERNEL_DEVICETREE_imx8mmsolidrun = ""
# avoid the new DTB added meta-lmp for imx8mm-evk
KERNEL_DEVICETREE_remove_imx8mmsolidrun = " freescale/imx8mm-evkb.dtb"
# Make sure we build the devicetree and add it to lmp-base builds
EXTRA_IMAGEDEPENDS_append = " virtual/dtb "
IMAGE_BOOT_FILES_append_lmp-base = " \
        devicetree/freescale_imx8mm-solidrun.dtb;imx8mm-solidrun.dtb \
"

# Add WiFi/BT firmware
MACHINE_FIRMWARE_append = " \
    linux-firmware-cyw-bt-patch-bcm43455 \
    linux-firmware-cyw-fmac-fw-bcm43455-sdio \
    linux-firmware-cyw-fmac-nvram-bcm43455-sdio \
"

# SolidRun Hummingboard Pulse has a Murata 1MW wifi/bt module which uses custom recipes
# linux-firmware-cyw-fmac-fw, linux-firmware-cyw-fmac-nvram and linux-firmware-cyw-bt-patch.
# Make sure we avoid default linux bcm43455 firmware
CORE_IMAGE_BASE_INSTALL_remove_imx8mmsolidrun = " \
    linux-firmware-bcm43455 \
"
